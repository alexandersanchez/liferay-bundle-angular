plugins {
  id "org.dm.bundle" version "0.8.5"
  id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

dependencies {
	compile 'com.liferay.portal:com.liferay.portal.kernel:2.3.0'
	compile 'com.liferay.portal:com.liferay.portal.impl:2.3.0'
	
	compile 'javax.portlet:portlet-api:2.0'
	compile 'javax.servlet:servlet-api:2.5'
	
	compile 'org.osgi:org.osgi.core:6.0.0'
	compile 'org.osgi:org.osgi.compendium:5.0.0'
}

group = 'com.rafoli'
version = '0.1.0'

bundle {
    instructions << [
        '-includeresource:': 'META-INF/resources=src/main/resources/META-INF/resources',
        'Web-ContextPath'  : '/liferay-bundle-angular',
    ]
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar 
            artifact javadocJar
        }
    }
}

bintray {   
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'liferay-bundle-angular'
		desc = 'Liferay 7 or DXP, AngularJS, Angular'
		licenses = ['MIT']
		vcsUrl = 'http://github.com/rafoli/liferay-bundle-angular.git'
		labels = ['liferay', 'angular', 'angularjs', 'dxp']
		publicDownloadNumbers = true
	    version {
	        name = project.version
	        desc = 'Liferay 7 or DXP, AngularJS, Angular'
	        released  = new Date()
	        vcsTag = project.version
	    }
	}
}